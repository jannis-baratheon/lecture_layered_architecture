plugins {
    id 'org.springframework.boot' apply false
    id 'io.spring.dependency-management' apply false
}

subprojects.forEach { proj ->
    proj.apply plugin: 'java'
    proj.apply plugin: 'org.springframework.boot'
 	proj.apply plugin: 'io.spring.dependency-management'

    proj.group = 'com.example'
    proj.version = '0.0.1-SNAPSHOT'

    proj.repositories {
        mavenCentral()
    }

    proj.dependencies {
        implementation "com.google.guava:guava:${getProperty('versions.guava')}"

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation "org.assertj:assertj-core:${getProperty('versions.assertj')}"
        testImplementation "org.mockito:mockito-core:${getProperty('versions.mockito')}"
        // Mockito needs it
        testRuntimeOnly "net.bytebuddy:byte-buddy:${getProperty('versions.bytebuddy')}"
    }

    proj.tasks.withType(Test).configureEach {
        it.useJUnitPlatform()
    }

    proj.tasks.withType(JavaCompile).configureEach {
        it.options.with {
            it.encoding = 'UTF-8'
            it.compilerArgs << '-Xlint'
            it.deprecation = true
        }
    }

    proj.extensions.configure(JavaPluginExtension) {
        javaPluginExtension ->
            javaPluginExtension.toolchain {
                toolchain ->
                    toolchain.languageVersion = JavaLanguageVersion.of(17)
            }
    }
}
